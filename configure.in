dnl ######################################################################
dnl ## Filename:      configure.in
dnl ## Version:       $Id$
dnl ## Description:   autoconf makefile
dnl ## Author:        $Author$
dnl ## Created at:    $Date$
dnl ######################################################################

dnl Process this file with autoconf to produce a configure script.
dnl Variables to be edited to meet your needs: AC_INIT, AM_INIT_AUTOMAKE
dnl and AC_OUTPUT. You should not have to edit the rest of the file.

dnl A source file from your development tree
AC_INIT(pkg/preprocess/source/preprocess.cxx)

AC_CANONICAL_SYSTEM 

dnl Project name and version
CVSVERS=`echo '$Name$' | sed 's/.*://;s/..$//'`
if test -z $CVSVERS; then
        CVSVERS=developer
fi
AM_INIT_AUTOMAKE(Ccd, $CVSVERS) 

dnl Checks for programs ==============================
AC_PROG_CXX
AC_PROG_CC
AC_PROG_F77
AC_SUBST(C_OPT)
AC_SUBST(C_ADDOPT)
case $build_os in
dnl        linux* ) C_OPT=-DUNDER ; C_ADDOPT="-fPIC -pipe -finline-functions -Winline -O";;
        linux* ) C_OPT=-DUNDER ; C_ADDOPT="-fPIC -pipe";;
        aix* ) C_OPT= ;;
        solaris* ) C_OPT=-DUNDER;;
        darwin* ) C_OPT=-DUNDER;;
esac 
AC_PROG_GCC_TRADITIONAL
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_RANLIB
dnl AM_PROG_LEX
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_F77_LIBRARY_LDFLAGS

dnl 
case $build_os in
darwin* ) LDF="$LDF -bind_at_load" ;;
esac

dnl creating dynamic modules
AC_SUBST(SH_FLAG)
AC_SUBST(SH_EXT)
case $build_os in
linux* ) SH_FLAG=-shared; SH_EXT=so;;
aix* ) SH_FLAG=-shared; SH_EXT=so;;
solaris* ) SH_FLAG=-shared; SH_EXT=so;;
darwin* ) SH_FLAG="-bundle -flat_namespace -undefined suppress"; SH_EXT=dylib;;
cygwin* ) SH_FLAG="-shared"; SH_EXT=dll;;
esac
                        
dnl Checks for libraries ==============================

dnl IFU_C_iolibs, >= 6.2 ------------------------------
dnl AC_HELP_STRING valid only for recent autoconfs

AC_MSG_CHECKING([for a valid IFU_C_iolibs])
AC_ARG_WITH(ifuciolibs, 
         [  --with-ifuciolibs=DIR       directory of IFU_C_iolibs ],
         [ac_ifuciolibs=$withval], 
         [ac_ifuciolibs=])
dnl If the user does not provide one, try to see if somthing acceptable is found
ac_ifuciolibs_vers_ok_list="6.2"

dnl try to find a valid version (simplified now)
if test -z "$ac_ifuciolibs"
then
    cwd=`pwd` 
    ac_ifuciolibs=$cwd/../IFU_C_iolibs
    if test ! -e "$ac_ifuciolibs" 
    then 
        AC_MSG_ERROR([No IFU_C_iolibs found. Use --with-ifuciolibs])
    fi
fi
dnl A version exists. Check now it is right (to improve)
ac_ifuciolibs_vers=` grep 'INIT(IFU_C_iolibs' ../IFU_C_iolibs/configure.ac | sed 's%.*s, *\(.*\), a.*)%\1%' `
for ac_ifuciolibs_vers_ok in $ac_ifuciolibs_vers_ok_list
do
    if test "$ac_ifuciolibs_vers" = "$ac_ifuciolibs_vers_ok"
    then
        ac_ifuciolibs_vers_ok="OK"
    fi
done
if test -z "$ac_ifuciolibs_vers_ok"
then
    AC_MSG_ERROR([IFU_C_iolibs version $ac_ifuciolibs_vers found - you should use one of $ac_ifuciolibs_vers_ok_list. Use --with-ifuciolibs or mailto:gangler@ipnl.in2p3.fr])
else
   AC_MSG_RESULT([version $ac_ifuciolibs_vers in $ac_ifuciolibs])
fi
LIBS="$LIBS -L$ac_ifuciolibs/C_libs/io/obj $ac_ifuciolibs/C_libs/obj/libgen.a  $ac_ifuciolibs/C_libs/wcslib/libwcs.a"
AC_SUBST(ac_ifuciolibs)
AC_CHECK_LIB(io,open_frame,
        [LIBS="$LIBS -lio"],
        [AC_MSG_ERROR(Unable to locate open_frame in $ac_ifuciolibs)],"-lm")

dnl AC_CHECK_LIB(io,open_frame,
dnl        [LIBS="$LIBS -lio"
dnl         C_OPT="$C_OPT -I$ac_ifuciolibs/incl"],
dnl        [AC_MSG_ERROR(Unable to locate open_frame in $ac_ifuciolibs)],"-lm")

dnl checking now for the mathlibs -------------------------------------
dnl needed marginally for technical reasons

dnl IFU_C_iolibs, >= 6.2 ------------------------------
dnl AC_HELP_STRING valid only for recent autoconfs

AC_MSG_CHECKING([for a valid IFU_C_mathlibs])
AC_ARG_WITH(ifucmathlibs, 
         [  --with-ifucmathlibs=DIR       directory of IFU_C_mathlibs ],
         [ac_ifucmathlibs=$withval], 
         [ac_ifucmathlibs=])
dnl If the user does not provide one, try to see if somthing acceptable is found
ac_ifucmathlibs_vers_ok_list="6.0 6.1 6.2"


dnl try to find a valid version (simplified now)
if test -z "$ac_ifucmathlibs"
then
    cwd=`pwd` 
    ac_ifucmathlibs=$cwd/../IFU_C_mathlibs
    if test ! -e "$ac_ifucmathlibs" 
    then 
        AC_MSG_ERROR([No IFU_C_mathlibs found. Use --with-ifucmathlibs])
    fi
fi
dnl A version exists. Check now it is right (to improve)
ac_ifucmathlibs_vers=` grep 'INIT(IFU_C_mathlibs' ../IFU_C_mathlibs/configure.ac | sed 's%.*s, *\(.*\), a.*)%\1%' `
for ac_ifucmathlibs_vers_ok in $ac_ifucmathlibs_vers_ok_list
do
    if test "$ac_ifucmathlibs_vers" = "$ac_ifucmathlibs_vers_ok"
    then
        ac_ifucmathlibs_vers_ok="OK"
    fi
done
if test -z "$ac_ifucmathlibs_vers_ok"
then
    AC_MSG_ERROR([IFU_C_mathlibs version $ac_ifucmathlibs_vers found - you should use one of $ac_ifucmathlibs_vers_ok_list. Use --with-ifucmathlibs or mailto:gangler@ipnl.in2p3.fr])
else
   AC_MSG_RESULT([version $ac_ifucmathlibs_vers in $ac_ifucmathlibs])
fi
AC_SUBST(ac_ifucmathlibs)

dnl checking now for the GSL -----------------------------------------------

AC_MSG_CHECKING([for a GSL lib])
AC_ARG_WITH(gsl, 
         [  --with-gsl=DIR       directory of GSL ],
         [ac_gsl=$withval], 
         [ac_gsl=])

dnl If the user does not provide one, try to see if somthing acceptable is found
echo ac_gsl : "$ac_gsl"
if test -z "$ac_gsl"
then
    AC_CHECK_LIB(gsl,main)
else
    AC_CHECK_LIB(gsl,main,[LIBS="$LIBS `$ac_gsl/bin/gsl-config --libs`" ;C_OPT="$C_OPT `$ac_gsl/bin/gsl-config --cflags`" ],
    [ AC_MSG_ERROR([No GSL libs in path ... provide one with --with-gsl])],
    [ `$ac_gsl/bin/gsl-config --libs`])
fi

dnl AC_SUBST(ac_gsl)
AC_MSG_RESULT(ok)

dnl AC_CHECK_LIB(gsl,gsl_stats_sd_m,
dnl         [],
dnl         [AC_MSG_ERROR(Unable to locate gsl_stats_sd_m in $ac_gsl - try SNIFS custom mathlib installation)],
dnl        [-L$ac_gsl/lib -lgslcblas -lm])

dnl Checking for ROOT system -----------------------------
AC_MSG_CHECKING(for ROOT system)
if test -n "$ROOTSYS"
then
    PKG_ROOT=anal
    AC_MSG_RESULT($ROOTSYS)
else
    PKG_ROOT=
    AC_MSG_RESULT([No \$ROOTSYS])
fi
AC_SUBST(PKG_ROOT)
dnl echo setting PKG_ROOT to $PKG_ROOT

dnl Checking for dvipdf -----------------------------
AC_CHECK_PROG(DVIPDF, dvipdf, yes, no)
if test "x$DVIPDF" = "xyes"; then
   DVIPDF=dvipdf
else 
   DVIPDF=echo
fi
AC_SUBST(DVIPDF)

dnl Checks for header files ==============================
AC_HEADER_STDC
AC_CHECK_HEADERS(malloc.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics ==========
AC_HEADER_TIME

dnl Checks for library functions ==============================
AC_CHECK_FUNCS(select strerror strspn strstr)

dnl miscellanous checks 

dnl ctags - does not work well on any platform ...
AC_CHECK_PROG(TAGS,ctags,ctags,no)
if test x$TAGS = xno; then
        AC_MSG_WARN([cannot create TAGS tables])
fi


dnl Extra options ==============================

dnl DEBUG mode ------------------------------
AC_SUBST(DEBUG)
AC_SUBST(STRIP)
AC_MSG_CHECKING(for DEBUG mode)
AC_ARG_ENABLE(debug,
        [  --enable-debug          DEBUG mode compilation - default is YES],
        [ac_debug=$withval],
        [ac_debug=yes]
)
if test "$ac_debug" = "yes"
then
  STRIP=@echo
  DEBUG="-g -Wall"
else
  DEBUG="-O"
  STRIP=strip
fi
AC_MSG_RESULT($ac_debug)

dnl PROFILING mode ------------------------------
AC_SUBST(LDF)
AC_MSG_CHECKING(for profiling mode)
AC_ARG_ENABLE(profile,
        [  --enable-profile        profiling mode compilation (gprof)],
        [ac_profile="-pg"],
        [ac_profile=no]
)
AC_MSG_RESULT($ac_profile)
if test x$ac_profile != xno; then
        if test x$ac_debug = xno; then
                AC_MSG_ERROR([Use --enable-profile with --enable-debug])
        fi
        AC_MSG_WARN([IFU/LCL I/O library has to be gprof-compiled too])
        LDF="$LDF $ac_profile"
        DEBUG="$DEBUG $ac_profile"
        ac_debug="$DEBUG"
fi

dnl Output ==============================

AC_OUTPUT(add_defs/makedefs \
          Makefile \
          pkg/Makefile \
          pkg/preprocess/Makefile \
          pkg/preprocess/lib/makefile \
          pkg/preprocess/source/makefile \
          pkg/anal/Makefile \
          pkg/anal/lib/makefile \
          pkg/anal/source/makefile \
          doc/makefile )

dnl Final configuration ==============================

dnl OUTPUT=`find . -name '?akefile.??'|sed 's|^\./||;s|\...$| \\\\|'|uniq|sort|sed '$s|\\\\$||'`

echo "
=============== Ccd final configuration ===============
Project:        $PACKAGE
Version:        $VERSION

If all settings are OK, type:
   make                                to compile the project
   make DEBUG=\"-g -Wall\" STRIP=@echo   to compile in debug-mode
   make dist                           to create a stand-alone tarball
=========================================================="

